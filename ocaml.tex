\section{\OCaml extensions for fine-grained concurrent programming}
\label{sec:ocaml}

Over the course of this work, we studied efficient fine-grained concurrent \OCaml programs written by experts.
This revealed various limitations of \OCaml in these domains, that those experts would work around using unsafe casts, often at the cost of both readability and memory-safety; and also some mismatches between their mental model of the semantics of \OCaml and the mental model used by the \OCaml compiler authors.
We worked on improving \OCaml itself to reduce these work-arounds or semantic mismatches.

\subsection{Atomic record fields}
\label{sec:atomic-record-fields}

\OCaml~5 offers a type \mintinline{ocaml}{'a Atomic.t} of atomic references exposing sequentially-consistent atomic operations.
Data races on non-atomic mutable locations has a much weaker semantics and is generally considered a programming error.
For example, the Michael-Scott concurrent queue~\cite{DBLP:conf/podc/MichaelS96} relies on a linked list structure that could be defined as follows:

\begin{minted}{ocaml}
type 'a node = Nil | Cons of { value : 'a; next : 'a node Atomic.t }
\end{minted}

Performance-minded concurrency experts dislike this representation, because \mintinline{ocaml}{'a Atomic.t} introduces an indirection in memory: it is represented as a pointer to a block containing the value of type \mintinline{ocaml}{'a}.
Instead, they use something like the following:

\begin{minted}{ocaml}
type 'a node = Nil | Cons of { mutable next: 'a node; value: 'a }
let as_atomic : 'a node -> 'a node Atomic.t option = function
  | Nil -> None
  | (Cons _) as record -> Some (Obj.magic record : 'a node Atomic.t)
\end{minted}

Notice that the \mintinline{ocaml}{next} field of the \mintinline{ocaml}{Cons} constructor has been moved first in the type declaration.
Because the \OCaml compiler respects field-declaration order in data layout, a value \mintinline{ocaml}!Cons { next; value }! has a similar low-level representation to a reference (atomic or not) pointing at \mintinline{ocaml}{next}, with an extra argument.
The code uses \mintinline{ocaml}{Obj.magic} to unsafely cast this value to an atomic reference, which appears to work as intended.

\mintinline{ocaml}{Obj.magic} is a shunned unsafe cast (the \OCaml equivalent of \mintinline{ocaml}{unsafe} or \mintinline{ocaml}{unsafePerformIO}).
It is very difficult to be confident about its usage given that it may typically violate assumptions made by the \OCaml compiler and optimizer.
In the example above, casting a two-fields record into a one-argument atomic reference may or may not be sound---but it gives measurable performance improvements on concurrent queue benchmarks.

It is possible to statically forbid passing \mintinline{ocaml}{Nil} to \mintinline{ocaml}{as_atomic} to avoid error handling, by turning \mintinline{ocaml}{'a node} into a GADT indexed over a type-level representation of its head constructor.
Examples of this pattern can be found in the \Kcas~\cite{kcas} library by Vesa Karvonen.
It is difficult to write correctly and use, in particular as unsafe casts can sometimes hide type-errors in the intended static discipline.

Note that this unsafe approach only works for the first field of a record, so it is not applicable to records that hold several atomic fields, such as the toplevel record storing atomic \mintinline{ocaml}{front} and \mintinline{ocaml}{back} pointers for the concurrent queue.

\subsubsection{Our atomic fields proposal}

We proposed a design for atomic record fields as an \OCaml language change proposal: RFC~\#39\footnote{\urlDeanonymizing{https://github.com/ocaml/RFCs/pull/39}}.
Declaring a record field atomic simply requires an \mintinline{ocaml}{[@atomic]} attribute---and could eventually become a proper keyword of the language.

\begin{minted}{ocaml}
(* re-implementation of atomic references *)
type 'a atomic_ref = { mutable contents : 'a [@atomic]; }

(* concurrent linked list *)
type 'a node = Nil | Cons of { value: 'a; mutable next : 'a node [@atomic]; }

(* bounded SPSC circular buffer *)
type 'a bag =
  { data : 'a Atomic.t array;
    mutable front: int [@atomic];
    mutable back: int [@atomic]; }
\end{minted}

The design difficulty is to express atomic operations on atomic record fields.
For example, if \mintinline{ocaml}{buf} has type \mintinline{ocaml}{'a bag} above, then one naturally expects the existing notation \mintinline{ocaml}{buf.front} to perform an atomic read and \mintinline{ocaml}{buf.front <- n} to perform an atomic write.
But how would one express exchange, compare-and-set and fetch-and-add?
We would like to avoid adding a new primitive language construct for each atomic operation.

Our proposed implementation\footnote{\urlDeanonymizing{https://github.com/ocaml/ocaml/pull/13404}}
introduces a built-in type \mintinline{ocaml}{'a Atomic.Loc.t}
for an atomic location that holds an element of type
\mintinline{ocaml}{'a}, with a syntax extension
\mintinline{ocaml}{[%atomic.loc <expr>.<field>]}
to construct such locations. Atomic primitives operate on values of type \mintinline{ocaml}{'a Atomic.Loc.t},
and they are exposed as functions of the module \mintinline{ocaml}{Atomic.Loc}.

For example, the standard library exposes
\begin{minted}{ocaml}
val Atomic.Loc.fetch_and_add : int Atomic.Loc.t -> int -> int
\end{minted}
and users can write:
\begin{minted}{ocaml}
let preincrement_front (buf : 'a bag) : int =
  Atomic.Loc.fetch_and_add [%atomic.loc buf.front] 1
\end{minted}
where \mintinline{ocaml}{[%atomic.loc buf.front]} has type \mintinline{ocaml}{int Atomic.Loc.t}.
Internally, a value of type \mintinline{ocaml}{'a Atomic.Loc.t} can be represented as a pair of a record and an integer offset for the desired field, and the \mintinline{ocaml}{atomic.loc} construction builds this pair in a well-typed manner.
When a primitive of the \mintinline{ocaml}{Atomic.Loc} module is applied to an \mintinline{ocaml}{atomic.loc} expression, the compiler can optimize away the construction of the pair---but it would happen if there was an abstraction barrier between the construction and its use.

Note: the type \mintinline{ocaml}{'a Atomic.t} of atomic references exposes a function
\begin{minted}{ocaml}
val Atomic.make_contended : 'a -> 'a Atomic.t
\end{minted}
that ensures that the returned atomic value is allocated with enough alignment and padding to sit alone on its cache line, to avoid performance issues caused by false sharing.
Currently there is no such support for padding of atomic record fields (we are planning to
work on this if the support for atomic fields gets merged in standard \OCaml), so the less-compact atomic references remain preferable in certain scenarios.

\subsection{Atomic arrays}

On top of our atomic record fields, we have implemented support for atomic arrays, another facility commonly requested by authors of efficient concurrent programs.
Our previous example of a concurrent bag of type \mintinline{ocaml}{'a bag} used a backing array of type \mintinline{ocaml}{'a Atomic.t array}, which contains more indirections than may be desirable, as each array element is a pointer to a block containing the value of type \mintinline{ocaml}{'a}, instead of storing the value of type \mintinline{ocaml}{'a} directly in the array.

Our implementation of atomic arrays\footnote{\urlAnonymous{https://github.com/clef-men/ocaml/tree/atomic\_array}} builds on top of the type \mintinline{ocaml}{'a Atomic.Loc.t} we described in the previous section, and it relies on two new low-level primitives provided by the compiler:

\begin{minted}{ocaml}
val Atomic_array.index : 'a array -> int -> 'a Atomic.Loc.t
val Atomic_array.unsafe_index : 'a array -> int -> 'a Atomic.Loc.t
\end{minted}

The function \mintinline{ocaml}{index} takes an array and an integer index within the array, and returns an atomic location into the corresponding element after performing a bound check.
\mintinline{ocaml}{unsafe_index} omits the boundcheck---additional performance at the cost of memory-safety---and allows to express the atomic counterpart of the unsafe operations \mintinline{ocaml}{Array.unsafe_get} and \mintinline{ocaml}{Array.unsafe_set}.
The atomic primitives of the module \mintinline{ocaml}{Atomic.Loc} can then be used on these indices; our implementation implements a library module on top of these primitives to provide a higher-level layer to the user, with direct array operations such as:

\begin{minted}{ocaml}
val Atomic_array.exchange : 'a Atomic_array.t -> int -> 'a -> 'a
val Atomic_array.unsafe_exchange : 'a Atomic_array.t -> int -> 'a -> 'a
\end{minted}
